class Tetris {
    static Tetris instance;
    static int DROP_COUNT;
    field int dropCount;

    field Board board;

    field char key;

    constructor Tetris new() {
        let DROP_COUNT = 10;
        let dropCount = 0;
        
	    do Screen.clearScreen();

        do Board.newInstance();
        let board = Board.getInstance();

        let key = 0;

        return this;
    }

    method void dispose() {
	    do board.dispose();
        do Memory.deAlloc(this);
        return;
    }

    function void newInstance() {
        let instance = Tetris.new();
        return;
    }
    
    function Tetris getInstance() {
        return instance;
    }

    method void draw() {
        do board.draw();
        return;
    }

    method void run() {
        while (true) {
            while (key = 0) { do run_(); }

            if (key = 140) { do gameOver(); }
            else { do board.setMovement(key); }

            while (~(key = 0)) { do run_(); }
        }
        return;
    }

    method void run_() {
        let key = Keyboard.keyPressed();
        let dropCount = dropCount + 1;
        if (dropCount > DROP_COUNT) {
            do board.drop();
            let dropCount = 0;
        }
        do board.draw();
        do Sys.wait(50);
        return;
    }

    method void gameOver() {
        do Output.moveCursor(11, 42);
        do Output.printString("Game Over");
        do Sys.halt();
        return;
    }
}
