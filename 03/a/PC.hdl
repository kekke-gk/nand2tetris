// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with increment, load, and reset modes.
 * if      (inc(t))   out(t+1) = out(t) + 1
 * else if (load(t))  out(t+1) = in(t)
 * else if (reset(t)) out(t+1) = 0
 * else               out(t+1) = out(t)
 *
 * To select a mode, assert the relevant control bit,
 * and de-assert the other two bits.
 */
/**
 * As stated in the official guideline, the above comment is incorrect.
 * The following is correct.
 *
 * A 16-bit counter.
 * if reset(t): out(t+1) = 0
 * else if load(t): out(t+1) = in(t)
 * else if inc(t): out(t+1) = out(t) + 1
 * else out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];

    PARTS:
    Inc16(in=pout, out=inc0);
    Mux16(a=pout, b=inc0, sel=inc, out=out2);
    Mux16(a=out2, b=in, sel=load, out=out1);
    Mux16(a=out1, b[0..15]=false, sel=reset, out=out0);
    Register(in=out0, load=true, out=pout, out=out);
}
